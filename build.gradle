allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "io.molr"

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://dl.bintray.com/molr/molr-repo" }
        maven { url "https://dl.bintray.com/minifx/minifx-repo" }
    }


    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

}

subprojects {
    jacocoTestReport {

        reports {
            html.enabled = false
            xml.enabled = true
            xml.destination file("$buildDir/reports/jacoco/report.xml")
            csv.enabled = false
        }
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }
task projectJar(type: Jar, dependsOn: subprojects.jar) {

    subprojects.each { subproject ->
        from subproject.configurations.runtime.allArtifacts.files.collect({ zipTree(it) })
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


task zip(type: Zip, dependsOn: projectJar) {
    from configurations.runtime.allArtifacts.files
    subprojects.each { subproject ->
        from subproject.configurations.runtime.filter { !it.path.contains("molr") }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
